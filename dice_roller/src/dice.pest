WHITESPACE = _{ " " }

expression = { SOI ~ operation ~ EOI }
operation = { term ~ (operator ~ term)* }
term = { dice_roll | function | number | "(" ~ operation ~ ")" }

function = { function_name ~ "(" ~ operation ~ ")" }
function_name = { "floor" | "round" | "ceil" | "abs" }

dice_roll = { number? ~ "d" ~ sides ~ modifier* }
sides = @{ "%" | number }
number = @{ ASCII_DIGIT+ }

modifier = { keep | drop | reroll | exploding | target_number }
keep = { keep_type ~ number? }
keep_type = { "k" | "kh" | "kl" }
drop = { drop_type ~ number? }
drop_type = { "d" | "dh" | "dl" }
reroll = { "r" ~ number? }
exploding = { "!" ~ number? }
target_number = { (">=" | "<=" | "=" | ">" | "<") ~ number }

operator = { "**" | "+" | "-" | "*" | "/" | "%" }
